// Copyright 2006-2010 (C) - Frictional Games
//
// This file is part of HPL1 Engine
//
// For conditions of distribution and use, see copyright notice in LICENSE-shaders
//

///////////////////////////////////////////////////////
/// DIFFUSE SPECULAR SPOTLIGHTING FRAGMENT PROGRAM /////////////
///////////////////////////////////////////////////////

in vec4 vLightColor;
in vec2 vUv;
in vec3 vLightDir;
in vec3 vHalfVec;
in vec4 vSpotlightUv;

OUTPUT

uniform sampler2D tex0; //diffuseMap
uniform sampler1D tex3; //falloffMap
uniform sampler2D tex4; //spotlightMap
uniform sampler1D tex5; //spotNegRejectMap

void main()
{
	vec3 diffuse = texture(tex0, vUv).xyz;

	vec3 lightNormal = normalize(vLightDir);
	vec3 halfVec = normalize(vHalfVec);

	float attenuation = texture1D(tex3,dot(vLightDir,vLightDir)).x;
	vec4 spotColor = texture(tex4, vSpotlightUv.xy / vSpotlightUv.w);
	float rejectNeg = texture1D(tex5,vSpotlightUv.z + 0.5).x;

	float specular = clamp(dot(halfVec, vec3(0,0,1)), 0.0, 1.0);
	specular = pow(specular, 16.0) * vLightColor.w * spotColor.w;

	outColor.xyz = specular +	diffuse * dot(lightNormal, vec3(0,0,1)) * vLightColor.xyz * spotColor.xyz;
	outColor.xyz *= attenuation * rejectNeg;
}
